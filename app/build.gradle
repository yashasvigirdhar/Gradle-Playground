
apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply from: '../gitutils.gradle'


def VERSION_PROPERTIES_FILE_NAME = "version.properties"
def VERSION_CODE_KEY = "VERSION_CODE"
def VERSION_PROPERTIES_PATH = "app/version.properties"

android {
    compileSdkVersion 27

    def versionPropsFile = file(VERSION_PROPERTIES_FILE_NAME)
    Properties versionProps = new Properties()
    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
        def code = versionProps[VERSION_CODE_KEY].toInteger()
        defaultConfig {
            applicationId "com.yashasvi.gradleplayground"
            minSdkVersion 26
            targetSdkVersion 27
            versionCode code
            versionName "1.0"
            setProperty("archivesBaseName", "v$versionName-" + "$code-" + getBranchName())
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
    } else {
        throw new GradleException("version properties file not found")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

task updateVersionCode() {
    group 'GradlePlayground'
    description 'updates version code in a separate commit'
    doLast {
        def versionPropsFile = project.file(VERSION_PROPERTIES_FILE_NAME)
        Properties versionProps = new Properties()
        if (versionPropsFile.canRead()) {
            versionProps.load(new FileInputStream(versionPropsFile))
            def code = versionProps[VERSION_CODE_KEY].toInteger() + 1
            versionProps[VERSION_CODE_KEY] = code.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new GradleException("can't find version.properties file")
        }
    }
}

task commitVersionProperties() {
    group 'GradlePlayground'
    description 'commits unsaved changes in version.properties'
    doLast {
        def versionPropsFile = project.file(VERSION_PROPERTIES_FILE_NAME)
        Properties versionProps = new Properties()
        if (versionPropsFile.canRead()) {
            versionProps.load(new FileInputStream(versionPropsFile))
            def code = versionProps[VERSION_CODE_KEY].toInteger()
            grgit.add(patterns: [VERSION_PROPERTIES_PATH])
            grgit.commit(message: "Update version code to $code")
            grgit.push()
            print "version code: $code updated successfully"
        } else {
            throw new GradleException("can't find version.properties file")
        }
    }
}

updateVersionCode.finalizedBy commitVersionProperties